!
!        Copyright (C) 2000-2019 the YAMBO team
!              http://www.yambo-code.org
!
! Authors (see AUTHORS file for details): AM, DS
! 
! This file is distributed under the terms of the GNU 
! General Public License. You can redistribute it and/or 
! modify it under the terms of the GNU General Public 
! License as published by the Free Software Foundation; 
! either version 2, or (at your option) any later version.
!
! This program is distributed in the hope that it will 
! be useful, but WITHOUT ANY WARRANTY; without even the 
! implied warranty of MERCHANTABILITY or FITNESS FOR A 
! PARTICULAR PURPOSE.  See the GNU General Public License 
! for more details.
!
! You should have received a copy of the GNU General Public 
! License along with this program; if not, write to the Free 
! Software Foundation, Inc., 59 Temple Place - Suite 330,Boston, 
! MA 02111-1307, USA or visit http://www.gnu.org/copyleft/gpl.txt.
!
subroutine INTERPOLATION_driver_seed(operation,ID,dims,SEED_k,SEED_E,SEED_RT,SEED_QP,SEED_R3,&
&                                    NEAREST,QUIET,SERIAL)
 !
 use pars,                 ONLY:SP
 use real_time,            ONLY:RT_t
 use QP_m,                 ONLY:QP_t
 use R_lattice,            ONLY:bz_samp
 use electrons,            ONLY:levels,n_sp_pol
 use interpolate_tools,    ONLY:init_ID,FILL_SEED_E,FILL_SEED_RT,FILL_SEED_QP
 use interpolate,          ONLY:INTERP_obj,INTERPOLATION_coefficients,max_interpls,&
&                               l_INTERP_f,l_INTERP_E,l_INTERP_dE,l_INTERP_W,INTERPOLATE_is_serial,&
&                               INTERPOLATE_initialized,l_INTERP_Z,INTERP_mode,INTERPOLATE_is_quiet
 use IO_m,                 ONLY:IO_and_Messaging_switch
 !
 implicit none
 !
 character(*),  intent(in)           :: operation
 integer,       intent(out)          :: ID
 integer,       intent(in)           :: dims(2)
 type(bz_samp), intent(in)           :: SEED_k
 type(RT_t),    intent(in), optional :: SEED_RT
 type(levels),  intent(in), optional :: SEED_E
 type(QP_t),    intent(in), optional :: SEED_QP
 real(SP),      intent(in), optional :: SEED_R3(:,:,:)
 logical,       intent(in), optional :: NEAREST
 logical,       intent(in), optional :: QUIET
 logical,       intent(in), optional :: SERIAL
 !
 ! Work Space
 !
 integer               :: i_dim,i_obj
 !
 ! Quiet?
 !========
 if (present(QUIET)) INTERPOLATE_is_quiet=QUIET
 if (INTERPOLATE_is_quiet) call IO_and_Messaging_switch("-log")
 !
 ! Serial?
 !=========
 if (present(SERIAL)) INTERPOLATE_is_serial=SERIAL
 !
 ! Actions
 !=========
 !
 l_INTERP_f =operation(1:1)== "f".or.operation(1:2)== "df"
 l_INTERP_E =operation(1:1)== "E"
 l_INTERP_Z =operation(1:1)== "Z"
 l_INTERP_W =operation(1:1)== "W"
 l_INTERP_dE=operation(1:2)== "dE"
 !
 if (index(operation,"_from_DB")>0) then
   l_INTERP_dE =index(operation(1:3),"dE")>0
   l_INTERP_W  =index(operation(1:3),"W")>0
   l_INTERP_Z  =index(operation(1:3),"Z")>0
 endif
 !
 ! ID to use
 !===========
 !
 ID=-1
 do i_obj=1,max_interpls
   if (INTERP_obj(i_obj)%in_use) cycle
   ID=i_obj
   exit
 enddo
 !
 ! SEED
 !======
 !
 if (present(SEED_E))  INTERP_obj(ID)%D=n_sp_pol
 if (present(SEED_RT)) INTERP_obj(ID)%D=n_sp_pol
 if (present(SEED_QP)) then
   INTERP_obj(ID)%D=0
   if (l_INTERP_dE) INTERP_obj(ID)%D=INTERP_obj(ID)%D+n_sp_pol
   if (l_INTERP_W ) INTERP_obj(ID)%D=INTERP_obj(ID)%D+n_sp_pol
   if (l_INTERP_Z ) INTERP_obj(ID)%D=INTERP_obj(ID)%D+n_sp_pol*2
 endif
 if (present(SEED_R3)) INTERP_obj(ID)%D=size(SEED_R3(1,1,:))
 !
 call init_ID(ID,dims,SEED_k,operation)
 !
 if (present(NEAREST)) then
   INTERP_obj(ID)%use_NN=NEAREST 
   if (NEAREST) INTERP_mode="NN"
 endif
 !
 if (.not.INTERPOLATE_initialized.and..not.INTERP_obj(ID)%use_NN) then
   !
   call INTERPOLATION_BZ_setup(SEED_k)
   !
   INTERPOLATE_initialized=.TRUE.
   !
 endif
 !
 ! I asssume here that SEED_E and SEED_RT lives on the same grid of SEED_k
 !
 if (present(SEED_E))   call FILL_SEED_E(ID,SEED_E)
 if (present(SEED_RT))  call FILL_SEED_RT(ID,SEED_RT)
 if (present(SEED_QP))  call FILL_SEED_QP(ID,SEED_QP)
 !
 if (present(SEED_R3)) then
   do i_dim=1,dims(2)-dims(1)+1
     INTERP_obj(ID)%SEED(dims(1)+i_dim-1,:,:)=SEED_R3(i_dim,:,:)
   enddo
 endif
 !
 if (INTERP_obj(ID)%use_NN) return
 !
 call INTERPOLATION_coefficients(R2D=INTERP_obj(ID)%SEED,k=SEED_k,ID=INTERP_obj(ID)%ID,ID_obj=ID)
 !
end subroutine INTERPOLATION_driver_seed
!
subroutine INTERPOLATION_driver_do(operation,ID,OUT_k,OUT_E)
 !
 use R_lattice,            ONLY:bz_samp
 use electrons,            ONLY:levels
 use interpolate_tools,    ONLY:INTERPOLATION_nearest_k_report
 use interpolate,          ONLY:INTERP_obj,INTERPOLATE_is_quiet,&
&                               INTERPOLATION_BZ,INTERPOLATION_nearest_k,&
&                               l_INTERP_f,l_INTERP_E,l_INTERP_dE,&
&                               l_integral_respect
 use IO_m,                 ONLY:IO_and_Messaging_switch
 !
#include<memory.h>
 !
 character(*),  intent(in)              :: operation
 integer,       intent(in)              :: ID
 type(bz_samp), intent(inout)           :: OUT_k
 type(levels),  intent(inout), optional :: OUT_E
 !
 ! Work Space
 !
 integer               :: n(2),nk
 !
 if (operation/=trim(INTERP_obj(ID)%what)) call error(' Interopolation ID did not match operation name')
 !
 ! ... Allocation
 !
 n =INTERP_obj(ID)%nb
 nk=INTERP_obj(ID)%nk
 !
 YAMBO_ALLOC(INTERP_obj(ID)%INTERP,(n(1):n(2),OUT_k%nibz,INTERP_obj(ID)%D))
 !
 ! ... Interpolation...
 !
 if (INTERP_obj(ID)%use_NN) then
   ! ... Using the Nearest point method
   call INTERPOLATION_nearest_k(INTERP_obj(ID)%k,INTERP_obj(ID)%SEED,OUT_k%pt,INTERP_obj(ID)%INTERP)
   if (.not.INTERPOLATE_is_quiet) call INTERPOLATION_nearest_k_report(ID)
   !
 else
   !
   ! ... Using BOLTZ method
   call INTERPOLATION_BZ(K=OUT_k,R2D=INTERP_obj(ID)%INTERP,ID=INTERP_obj(ID)%ID)
   !
 endif
 !
 ! f/dF normalization
 !====================
 if (l_INTERP_f) call INTERPOLATION_integral_respect(ID,OUT_k,INTERP_obj(ID)%INTERP,l_integral_respect)
 !
 ! store f/E/dE in OUT_E
 !=======================
 if (present(OUT_E)) then
   if (l_INTERP_E.or.l_INTERP_dE) then
     YAMBO_ALLOC(OUT_E%E,(n(1):n(2),OUT_k%nibz,INTERP_obj(ID)%D))
   else if (l_INTERP_f) then
     YAMBO_ALLOC(OUT_E%f,(n(1):n(2),OUT_k%nibz,INTERP_obj(ID)%D))
   endif
   OUT_E%nk=OUT_k%nibz
   OUT_E%nb=INTERP_obj(ID)%nb(2)
   if (l_INTERP_E.or.l_INTERP_dE) OUT_E%E=INTERP_obj(ID)%INTERP
   if (l_INTERP_f)                OUT_E%f=INTERP_obj(ID)%INTERP
 endif
 !
 if (INTERPOLATE_is_quiet) call IO_and_Messaging_switch("+log")
 !
end subroutine INTERPOLATION_driver_do
!
subroutine INTERPOLATION_driver_create_grid(GRID)
 !
 use interpolate,          ONLY:GRID_k
 use R_lattice,            ONLY:bz_samp_reset
 !
 implicit none
 !
 integer, intent(in) :: GRID(3)
 !
 call bz_samp_reset(GRID_k)
 GRID_k%description="INTERP"
 call INTERPOLATION_create_the_grid(GRID_k,'i',GRID)
 !
end subroutine INTERPOLATION_driver_create_grid
!
subroutine INTERPOLATION_driver_end(ID)
 !
 use interpolate_tools,    ONLY:INTERPOLATION_reset
 use interpolate,          ONLY:INTERPOLATE_msg_fmt,INTERPOLATE_initialized,&
&                               lattice_vectors,INTERP_obj,max_interpls,int_sop
 !
#include<memory.h>
 !
 integer, intent(in) :: ID
 !
 ! Work space
 !
 integer             :: i_obj
 !
 if (ID>0) then
   !
   ! Reset only specific ID
   !
   if (.not.INTERP_obj(ID)%in_use) return 
   call INTERPOLATION_reset(ID)
   !
 else
   !
   ! Reset all IDs and clear lattice vectors
   !
   do i_obj=1,max_interpls
     call INTERPOLATION_reset(i_obj)
   enddo
   !
   YAMBO_FREE_P(lattice_vectors)
   !
   YAMBO_FREE(int_sop)
   INTERPOLATE_initialized=.FALSE.
   INTERPOLATE_msg_fmt="rs"
   !
 endif
 !
end subroutine INTERPOLATION_driver_end
