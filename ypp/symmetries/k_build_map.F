!
! License-Identifier: GPL
!
! Copyright (C) 2016 The Yambo Team
!
! Authors (see AUTHORS file for details): CA DS
!
subroutine k_build_map(k,k_save,kpoints_map)
 !
 use pars,                ONLY:SP
 use zeros,               ONLY:k_iku_zero    !,k_rlu_zero
 use vec_operate,         ONLY:c2a,v_is_zero !,rlu_v_is_zero
 use R_lattice,           ONLY:bz_samp
 !
 implicit none
 !
 type(bz_samp),intent(inout) :: k
 type(bz_samp),intent(in)    :: k_save
 integer,intent(out)         :: kpoints_map(2,k%nibz)
 !
 ! Work space
 !
 integer               :: i1,ik,ik_save
 integer               :: iv(2),nk
 real(SP)              :: v1(3),v2(3)
 ! 
 !
 ! Map k-point using the save BZ
 !
 ! kpoints_map(1, ik ) = ik_save
 ! kpoints_map(2, ik ) = is
 !
 ! with ik_save,is relative to the original set of k-points and symmetries
 !
 nk=k_save%nbz
 !
 kpoints_map=0
 !
 do ik=1,k%nibz 
   do ik_save=1,nk
     v1=k_save%ptbz(ik_save,:)
     !
     call c2a(v_in=v1,mode='ki2a')
     call c2a(v_in=k%pt(ik,:),v_out=v2,mode='ki2a')
     !
     ! 2022/05/25 DS: If we have problems with the following procedure,
     !                It means that we need to use the procedure in between DEBUG
     !                commands and track the Go shifts
     !
     ! DEBUG <
     !if (rlu_v_is_zero(v2-v1,zero_=k_rlu_zero)) then
     ! DEBUG >
     if ( v_is_zero(v1-v2,k_iku_zero) ) then
       kpoints_map(:, ik ) = k_save%sstar(ik_save,:2)
       exit 
     endif
   enddo
 enddo
 !
 if ( any(kpoints_map==0) ) call error(" kpoints map is not complete")
 !
 ! swap ik and i1 to sort
 ! first all the points in the ibz
 ! 
 main_loop: do ik=1,k%nibz 
   if ( kpoints_map(2, ik )/=1 ) then
     do i1=ik+1,k%nibz
       if ( kpoints_map(2, i1 )==1 ) then
         iv=kpoints_map(:, ik )
         v1=k%pt(ik,:)
         kpoints_map(:,ik)=kpoints_map(:,i1)
         kpoints_map(:,i1)=iv
         k%pt(ik,:)=k%pt(i1,:)                 
         k%pt(i1,:)=v1
         cycle main_loop
       endif
     enddo
   endif
 enddo main_loop
 !
end subroutine
